buildscript {
    repositories() {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'de.jjohannes.extra-java-module-info' version "0.1"
    id 'application'
}

application {
    mainClassName = 'org.jungletree.startup.Main'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    apply plugin: 'de.jjohannes.extra-java-module-info'

    repositories {
        jcenter()
        mavenCentral()
    }

    group 'org.jungletree'
    version '0.0.1-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14

    extraJavaModuleInfo {
        automaticModule("tomlj-1.0.0.jar", "tomlj")
    }

    java {
        modularity.inferModulePath = true
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.2'

    implementation project(':api')
    implementation project(':core')
    implementation project(':world')
    implementation project(':entity')
    implementation project(':net')
}

project(':net') {
    dependencies {
        compileOnly project(':api')
    }
}

project(':entity') {
    dependencies {
        compileOnly project(':api')
    }
}

project(':world') {
    dependencies {
        compileOnly project(':api')
    }
}

project(':core') {
    dependencies {
        compileOnly project(':api')
        compileOnly project(':world')
        compileOnly project(':entity')
        compileOnly project(':net')
    }
}
